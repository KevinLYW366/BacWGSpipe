#####################
# Step 05. Genotype #
#####################

##### 1. Multilocus sequence typing (MLST) #####
# mlst: multilocus sequence typing
rule mlst_run:
    input:
        # assembly contigs generated by Unicycler
        "results/02.assembly/{sample}/assembly/{sample}.fasta"
    output:
        # MLST report by mlst
        "results/05.genotype/{sample}/mlst/{sample}_mlst.txt"
    log:
        "logs/05.genotype/mlst/mlst_run/{sample}.log"
    threads:
        config["threads"]
    conda:
        "../envs/mlst.yaml"
    shell:
        """
        mlst --threads {threads} {input} > {output} 2> {log}
        """

# Merge MLST reports
rule mlst_merge:
    input:
        # MLST report by mlst
        expand("results/05.genotype/{sample}/mlst/{sample}_mlst.txt", sample=SAMPLES)
    output:
        # merged MLST report
        "results/05.genotype/all_mlst_results.xls"
    params:
        indir = "results/05.genotype",
        outdir = "results/05.genotype",
        sample_list = config["sample_list"],
        script = config["merge_results_script"]
    log:
        "logs/05.genotype/mlst/mlst_merge.log"
    shell:
        """
        python {params.script} -m mlst -i {params.indir} -o {params.outdir} -s {params.sample_list} > {log} 2>&1
        """
