##################################
# Step 08. Phylogenetic Analysis #
##################################

# Roary: generate core-genome alignment
rule roary:
    input:
        # the master annotation in GFF3 format, containing both sequences and annotations, generated by Prokka
        expand("results/03.genome_component/{sample}/prokka/{sample}.gff", sample=SAMPLES)
    output:
        # core-genome alignment which will be used to generate a phylotree in next step
        aln = "results/08.phylogenetics/roary/core_gene_alignment.aln",
        # a tree created using the binary presence and absence of accessory genes
        tree = "results/08.phylogenetics/roary/accessory_binary_genes.fa.newick",
        # the gene presence and absence spreadsheet lists each gene and which samples it is present in
        csv = "results/08.phylogenetics/roary/gene_presence_absence.csv",
        rtab = "results/08.phylogenetics/roary/gene_presence_absence.Rtab",
        # a FASTA file which contains a single representative nucleotide sequence
        #   from each of the clusters in the pan genome (core and accessory)
        fa = "results/08.phylogenetics/roary/pan_genome_reference.fa"
    params:
        outdir = "results/08.phylogenetics/roary",
    log:
        "logs/08.phylogenetics/roary.log"
    threads:
        64
    conda:
        "../envs/roary.yaml"
    shell:
        """
        # remove the folder automatically created by Snakemake 
        # otherwise Roary would add a timestamp to the existed folder's name
        rm -r {params.outdir}
        roary -e --mafft -p {threads} -f {params.outdir} -r {input} > {log} 2>&1
        """

# IQ-tree: generate a phylogenetic tree based on the core gene alignment identified by Roary
rule iqtree:
    input:
        # core gene alignment generated by Roary
        "results/08.phylogenetics/roary/core_gene_alignment.aln"
    output:
        # phylogenetic tree file
        "results/08.phylogenetics/iqtree/core_gene_alignment.treefile",
    params:
        prefix = "results/08.phylogenetics/iqtree/core_gene_alignment",
    log:
        "logs/08.phylogenetics/iqtree.log"
    threads:
        64
    conda:
        "../envs/iqtree.yaml"
    shell:
        """
        iqtree -s {input} -T {threads} -B 1000 -m TEST --prefix {params.prefix} --redo > {log} 2>&1
        """